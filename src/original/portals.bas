1 REM *M.PAGE "1987/8-PORTALS OF P'THAAL"*
2 CLEAR 64671
3 PRINT "LOADING CODE": LOAD "PORTCODE"CODE

4 REM *INITIALISATION*
5 LET A=0: LET B=1: LET C=2: LET D=3: LET E=4: LET F=5: LET G=6: LET H=7: LET J=8: LET K=9: LET M=10: LET Q=60
6 DIM Z(Q): DIM Z$(121,C,H): LET RL=A: LET W=920: LET SP=930: LET CH=A: LET COM=A
8 POKE 23658,J
10 BORDER A: PAPER G: CLS 
15 DEF FN R(R)=INT (RND*R+B)
20 RANDOMIZE : DIM L$(121,C,H): DIM N$(D,H): DIM M$(15,16): DIM P$(39,13): DIM S$(13,M): DIM U$(J,D): DIM K$(D,K)
25 LET Z1=23606: LET Z2=Z1+B
30 LET B$="2Fountain": LET C$="4Camp": LET D$="8Portal": LET J$="Wrong denizen!"
50 FOR N=B TO D: READ N$(N): NEXT N
55 FOR N=B TO 15: READ M$(N): NEXT N
60 FOR N=B TO 39: READ P$(N): NEXT N
65 FOR N=B TO 13: READ S$(N): NEXT N
70 FOR N=B TO J: READ U$(N): NEXT N
75 FOR N=B TO D: READ K$(N): NEXT N
100 PRINT FLASH B;AT M,M;"PLEASE WAIT"
102 LET QT=A: LET X=FN R(121): LET Y=B
105 DIM L(Q)
115 RESTORE 8410: FOR N=D TO H: READ L(N): NEXT N
120 FOR N=B TO D: LET V=FN R(13): LET L(40+V)=L(40+V)+B: NEXT N
210 FOR I=B TO C: FOR N=B TO 121
220 LET L$(N,I)="1111000"
230 IF FN R(C)=C THEN LET L$(N,I,FN R(E))="0"
240 NEXT N: NEXT I
310 FOR N=B TO K: GO SUB 340: LET L$(V,B,G)=STR$ N: NEXT N
320 FOR N=M TO 39: GO SUB 340: LET L$(V,B,F TO G)=STR$ N: NEXT N: GO TO 350
340 LET V=FN R(121): IF VAL L$(V,B,F TO G)<>A THEN GO TO 340
345 RETURN 
350 FOR N=B TO 121: IF FN R(15)=J THEN LET L$(N,C,F)="1"
360 NEXT N
370 FOR I=B TO C: FOR N=B TO M
375 LET V=FN R(121): IF L$(V,I,H)<>"0" THEN GO TO 375
380 LET L$(V,I,H)="1": NEXT N: NEXT I
390 FOR I=B TO C
400 FOR N=B TO 11: LET L$(N,I,B)="0": NEXT N
405 FOR N=111 TO 121: LET L$(N,I,C)="0": NEXT N
410 FOR N=B TO 111 STEP 11: LET L$(N,I,E)="0": NEXT N
415 FOR N=11 TO 121 STEP 11: LET L$(N,I,D)="0": NEXT N
420 NEXT I

425 REM *Choose character and experience points*
430 CLS : BEEP B,20: PRINT "Which character do you want to  play?"'''"1:";N$(B)'"2:";N$(C)'"3:";N$(D)'''"Press key 1,2 or 3"
435 LET I$=INKEY$
440 IF CODE I$<49 OR CODE I$>51 THEN GO TO 435
445 LET L(57)=VAL I$: PRINT '' N$(L(57)): BEEP B,20
450 CLS : PRINT "Enter experience points requiredfor victory (100 to 999)."
455 INPUT LINE A$: IF A$="" THEN GO TO 455
460 FOR N=B TO LEN A$: LET E$=A$(N): IF CODE E$<48 OR CODE E$>57 THEN GO TO 455
465 NEXT N
470 LET V=VAL A$: IF V<100 OR V>999 THEN GO TO 455
475 LET L(54)=V
480 PRINT '''V: BEEP B,20

499 REM *CREATE SCREEN*
500 PAPER G: CLS : LET QL=A: GO SUB 801: FOR N=B TO D STEP C: PRINT AT N,A;"MMMMMMMMMMMMMMMMMM": NEXT N
502 INK F: PAPER B
505 FOR N=A TO D: PRINT AT N,18;"N": NEXT N
507 FOR N=C TO D: PRINT AT N,H;"NN  N": NEXT N
510 FOR N=E TO 16: PRINT AT N,G;"N": NEXT N
512 FOR N=C TO 21: PRINT AT N,19;"N": NEXT N
515 FOR N=C TO J: PRINT AT N,31;"N": NEXT N
517 FOR N=11 TO 21: PRINT AT N,31;"N": NEXT N
520 LET A$="NNNNNNNNNNNN"
522 PRINT AT C,20;A$;AT J,20;A$;AT 11,20;A$;AT 21,20;A$;AT 17,A;A$;AT 17,J;A$
530 INK B: PAPER H:
532 LET V=L(57)*E: PRINT AT C,K;CHR$ (75+V);CHR$ (76+V);AT D,K;CHR$ (77+V);CHR$ (78+V): GO SUB 805
540 INK E: PAPER A
542 PRINT AT A,A; "PORTALS OF P'THAAL"
545 PRINT AT A,19;"VIC EXP:";AT B,19;"**TURNS:"
547 INK G: PAPER B
550 PRINT AT C,A;N$(L(57))
552 INK B: PAPER F
555 RESTORE 8500
557 FOR N=B TO H: READ A$: PRINT AT C+N*C,A;A$: NEXT N
562 PRINT INK H; PAPER C;AT A,27;L(54)
565 FOR N=B TO G: PRINT INK H; PAPER B;AT C+N*C,D;L(N+C): NEXT N: PRINT INK H; PAPER B;AT 16,D;"+";INT (L(56)/E)
570 INK A: PAPER G
572 PRINT AT C,12;"SPELLS"
575 FOR N=B TO 13
578 PRINT AT N+D,H;S$(N,B); INVERSE B;CHR$ (64+N); INVERSE A;S$(N,C TO );L(40+N): NEXT N
580 OVER B: PLOT 143,40: DRAW A,104: OVER A
590 GO SUB 820
600 GO SUB 900
798 GO TO 1999

799 REM *SUBROUTINES*

800 REM *CHOOSE CHAR SET*
801 POKE Z1,120: POKE Z2,252: RETURN
805 POKE Z1,A: POKE Z2,60: RETURN
815 POKE Z1,72: POKE Z2,253: RETURN

820 REM *DRAW MAP*
821 PAPER G: FOR N=K TO M: PRINT AT N,20;"            ": NEXT N
822 IF Y=C THEN INK H: PAPER A: LET A$="HHHHHHHHHHH": GO TO 835
824 IF X>0 THEN PAPER H: LET A$="IIIIIIIIIII"
826 IF X>33 THEN PAPER E: LET A$="FFFFFFFFFFF"
828 IF X>66 THEN PAPER F: LET A$="GGGGGGGGGGG"
830 IF X>99 THEN PAPER G: LET A$="HHHHHHHHHHH"
840 GO SUB 801: FOR N=D TO H: PRINT AT N,20;A$: NEXT N
842 PRINT AT F,25;"A"
844 IF L$(X,Y,B)="1" THEN PRINT AT D,25;"A";AT E,25;"A"
846 IF L$(X,Y,C)="1" THEN PRINT AT G,25;"A";AT H,25;"A"
848 IF L$(X,Y,D)="1" THEN PRINT AT F,26;"BBBBB"
850 IF L$(X,Y,E)="1" THEN PRINT AT F,20;"BBBBB"
851 INK A: PAPER G
852 LET V=VAL L$(X,Y,F TO G): IF NOT V THEN GO TO 855
854 IF V>35 THEN PRINT AT D,27;"E"
855 IF Y=C AND L$(X,Y,F)="1" THEN PRINT AT D,27;"D"
856 PRINT AT D,21;"J"
857 GO SUB 1140
858 GO SUB 801: PRINT AT H,27;"C"
860 LET I=A: IF V AND Y=B THEN LET I=VAL P$(V,B): GO SUB 815: PRINT AT H,20;U$(VAL P$(V,B))
868 GO SUB 805: PRINT AT H,28;L(56)
869 IF Y=C THEN PRINT AT K,20;"Netherworld": RETURN 
870 IF V THEN PRINT AT K,20;P$(V,C TO ): IF V<14 AND V<>A THEN PRINT AT M,21;"Guild"
890 RETURN

900 REM *MENU*
901 FOR N=12 TO 21: PRINT PAPER E;AT N,20;"           ": NEXT N
905 PRINT INVERSE B; INK H; PAPER E;AT 13,23;"MENU*"; INVERSE A;AT 15,20;"N*S*E*W*U*D";AT 16,23;"MAGIC";AT 17,23;"LOAD*";AT 18,21;"QUIT/SAVE";AT 19,21;"BEFRIEND*": RETURN 
920 FOR N=18 TO 21: PRINT PAPER H;AT N,A;"                   ": NEXT N: RETURN : REM CLEAR WINDOW*
930 BEEP .5,20: PRINT INK B; FLASH B;AT 21,14;"SPACE"
935 LET I$=INKEY$
940 IF I$=" " THEN BEEP .03,25: GO SUB W: RETURN 
945 GO TO 935: REM *"SPACE"*
950 IF L(J)>999 THEN LET L(J)=999: REM *CHECK MAX EXP*
955 RETURN

960 REM *INPUT ROUTINE*
962 GO SUB W: PRINT AT 19,E;"SEE MENU--->"
965 LET I$=INKEY$
970 IF I$="Q" THEN GO TO 3000
972 IF I$="L" THEN GO TO 3100
973 IF I$="B" THEN GO TO 4000
974 IF I$="M" THEN LET COM=A: GO TO 5000
980 IF I$="N" THEN GO TO 990
981 IF I$="S" THEN GO TO 1000
982 IF I$="E" THEN GO TO 1010
983 IF I$="W" THEN GO TO 1020
984 IF I$="U" THEN GO TO 1030
985 IF I$="D" THEN GO TO 1040
987 GO TO 965
990 IF L$(X,Y,B)="0" THEN GO TO 1050
992 IF L$(X-11,Y,C)="0" AND L(M)<B THEN GO TO 1055
995 LET X=X-11: GO TO 1060
1000 IF L$(X,Y,C)="0" THEN GO TO 1050
1002 IF L$(X+11,Y,B)="0" AND L(M)<B THEN GO TO 1055
1005 LET X=X+11: GO TO 1060
1010 IF L$(X,Y,D)="0" THEN GO TO 1050
1012 IF L$(X+B,Y,E)="0" AND L(M)<B THEN GO TO 1055
1015 LET X=X+B: GO TO 1060
1020 IF L$(X,Y,E)="0" THEN GO TO 1050
1022 IF L$(X-B,Y,D)="0" AND L(M)<B THEN GO TO 1055
1025 LET X=X-B: GO TO 1060
1030 IF Y<>C THEN GO TO 1050
1031 IF Y=C AND L$(X,Y,F)<>"1" THEN GO TO 1050
1032 LET Y=B: GO TO 1060
1040 IF Y<>B THEN GO TO 1050
1041 IF I<>J THEN GO TO 1050
1042 LET Y=C: GO TO 1060
1050 GO SUB W: PRINT AT 19,E;"No Pathway!": BEEP B,-20: GO SUB W: RETURN 
1055 GO SUB W: PRINT AT 19,D;"Blocked Path!": BEEP .05,D: BEEP .05,A: FOR N=B TO 50: NEXT N: GO SUB W: RETURN 
1060 GO SUB W: GO SUB 820: RETURN 
1100 LET L(B)=X: LET L(C)=Y: RETURN 
1110 LET X=L(B): LET Y=L(C): RETURN 
1120 IF L(J)>=L(54) THEN GO SUB W: FOR N=B TO 20: BEEP .02,N: NEXT N: PRINT AT 19,A;"You have won the"'"game!-SPACE to play"'"again": GO SUB SP: PAPER G: CLS : GO TO 100
1130 RETURN : REM *CHECK WIN*
1140 FOR N=A TO B: IF L(59+N) THEN GO SUB 801: PRINT PAPER G;AT D,22+N;CHR$ (75+N)
1145 NEXT N
1150 GO SUB 805: RETURN : REM *CHECK VILLAGER*

2000 REM *MAIN LOOP*
2005 PAPER H: FOR N=B TO H: BEEP .01,20: NEXT N
2010 LET L(58)=L(58)+B: PRINT INK H; PAPER C;AT B,27;L(58)
2020 FOR N=K TO 40: LET L(N)=L(N)-B: NEXT N
2025 PAPER H
2026 LET TEST=(H-L(59)-L(60))
2030 REM IF FN R(H-L(59)-L(60))=D AND L(K)<A THEN GO SUB 6000
2031 IF FN R(TEST)=D THEN GO SUB 6000

2200 REM *VARIOUS CHECKS*
2205 IF NOT I THEN GO TO 2300
2215 IF I=B THEN IF L(20+V)<A THEN LET L(40+V)=L(40+V)+B: LET L(20+V)=15: PRINT PAPER G;AT D+V,18;L(40+V): GO SUB W: PRINT AT 18,C;"You learn the";AT 19,C;S$(V,C TO );AT 20,C;"spell": GO SUB SP
2220 IF I=C THEN IF L(V+20)<A THEN LET L(V+20)=15: LET L(D)=L(D)+F: PRINT INK H; PAPER B;AT E,D;L(D): GO SUB W: PRINT AT 19,C;"You gain MAGic";AT 20,C;"at the fountain": GO SUB SP
2225 IF I=D THEN IF L(V-K)<A THEN LET L(V-K)=50: LET L(J)=L(J)+30: GO SUB 950: PRINT INK H; PAPER B;AT 14,D;L(J): GO SUB W: PRINT AT 19,C;"You gain";AT 20,C;"EXPerience": GO SUB SP: GO SUB 1120
2230 IF I=E THEN IF L(V-G)<A THEN LET L(V-G)=100: LET L(G)=L(G)+B: PRINT INK H; PAPER B;AT M,D;L(G): GO SUB W: PRINT AT 19,C;"You gain";AT 20,C;"PERsonality": GO SUB SP
2235 IF I=F AND NOT L(55) THEN LET L(55)=B: LET L(E)=L(E)+D: PRINT INK H; PAPER B;AT G,D;L(E): GO SUB W: PRINT AT 19,B;"You are blessed";AT 20,B;"by the Bishop";AT 21,B;"(+3 STRength)": GO SUB SP
2240 IF I=H THEN IF L(V-19)<A THEN LET L(V-19)=30: LET L(F)=L(F)+C: PRINT INK H; PAPER B;AT J,D;L(F): GO SUB W: PRINT AT 19,C;"You gain";AT 20,C;"ENDurance": GO SUB SP
2300 IF L$(X,Y,H)="1" THEN LET L(56)=L(56)+B: LET L$(X,Y,H)="0": PRINT PAPER G;AT H,28;L(56): GO SUB W: PRINT AT 19,C;"Eyestone found!": PRINT INK H; PAPER B;AT 16,D;"+";INT (L(56)/E): GO SUB SP
2310 GO SUB 960: PAPER H
2320 IF QT THEN PAPER G: CLS : GO TO 100
2330 IF QL THEN GO TO 500
2999 GO TO 2000

3001 REM *QUIT/SAVE/RAMSAVE*
3002 PAPER G: CLS : FOR N=B TO 50: NEXT N
3005 LET QL=B: PRINT AT E,M;"Q)uit"''TAB M;"S)ave"''TAB M;"R)amsave"''TAB M;"E)xit"
3010 LET I$=INKEY$
3015 IF I$="Q" THEN LET QT=B: RETURN 
3020 IF I$="S" THEN GO SUB 1100: CLS : PRINT FLASH B;AT E,M;"Saving Now": SAVE "P1" DATA L(): SAVE "P2" DATA L$(): RETURN 
3025 IF I$="R" THEN GO SUB 1100: CLS : PRINT FLASH B;AT E,K;"Ramsaving Now": FOR N=B TO Q: LET Z(N)=L(N): NEXT N: FOR T=B TO C: FOR N=B TO 121: LET Z$(N,T)=L$(N,T): NEXT N: NEXT T: LET RL=B: RETURN 
3030 IF I$="E" THEN RETURN 
3040 GO TO 3010

3101 REM *LOAD/RAMLOAD*
3102 PAPER G: CLS : FOR N=B TO 50: NEXT N
3105 CLS : LET QL=B: PRINT AT E,K;"L)oad Saved Game"''TAB K;"R)amload"''TAB K;"E)xit"
3110 LET I$=INKEY$
3115 IF I$="L" THEN CLS : PRINT FLASH B;AT E,G;"Loading Saved Game": LOAD "P1" DATA L(): LOAD "P2" DATA L$(): GO SUB 1110: RETURN 
3120 IF I$="R" AND NOT RL THEN CLS : PRINT AT E,H;"No Game Ramsaved": PAUSE 150: RETURN 
3125 IF I$="R" AND RL THEN CLS : PRINT FLASH B;AT E,J;"Loading Ramsave": FOR N=B TO Q: LET L(N)=Z(N): NEXT N: FOR T=B TO C: FOR N=B TO 121: LET L$(N,T)=Z$(N,T): NEXT N: NEXT N: GO SUB 1110: RETURN 
3130 IF I$="E" THEN RETURN 
3140 GO TO 3110

4000 REM *BEFRIEND*
4005 IF I<>G THEN GO SUB W: PRINT AT 19,C;"This isn't a";AT 20,C;"village!": GO SUB SP: RETURN 
4010 IF L(59) AND L(60) THEN GO SUB W: PRINT AT 19,B;"You've already got";AT 20,C;"two villagers!": GO SUB SP: RETURN 
4011 IF L(K)>A THEN PRINT AT 19,B;"You're invisible!": GO SUB SP: RETURN 
4012 IF CH THEN GO TO 4100
4015 IF L(40)>A THEN GO SUB W: PRINT AT 19,C;"There's no one";AT 20,C;"at home!": GO SUB SP: RETURN 
4020 LET L(40)=16: IF L(G)+FN R(G)>J THEN PRINT AT 19,C;"You befriend a";AT 20,C;"villager!": GO SUB 4100: GO SUB SP: RETURN 
4030 GO SUB W: PRINT AT 19,B;"Sorry,you weren't";AT 20,B;"friendly enough!": GO SUB SP: RETURN 
4100 IF NOT L(59) THEN LET L(59)=B: GO SUB 1140: RETURN 
4110 LET L(60)=B: GO SUB 1140: RETURN

5000 REM *MAGIC*
5005 GO SUB W: FOR N=B TO 50: NEXT N
5010 PRINT AT 19,C;"Choose a spell";AT 20,F;"(H to M)"
5015 LET I$=INKEY$
5020 IF CODE I$<72 OR CODE I$>77 THEN GO TO 5015
5030 LET U=(CODE I$-24): IF NOT L(U) THEN GO SUB W: PRINT AT 19,B;"Spell not learnt!": GO SUB SP: LET BAD=B: RETURN 
5035 LET T=(VAL S$(CODE I$-64,B)): IF T > L(D) THEN GO SUB W: PRINT AT 19,B;"Not enough magic!": GO SUB SP: LET BAD=B: RETURN 
5040 GO SUB W: PRINT AT 19,E;"Spell cast!": FOR Z=B TO D: FOR N=30 TO 20 STEP -1: BEEP .02,N: NEXT N: NEXT Z
5045 LET L(D)=L(D)-T: LET L(U)=L(U)-B: PRINT PAPER G;AT U-37,18;L(U); PAPER G;AT E,D;"   "; INK H; PAPER B;AT E,D;L(D)
5050 IF COM THEN RETURN 
5100 GO TO (CODE I$-71)*100+5100

5200 REM *CHARMER*
5205 LET CH=B: GO SUB 4000
5210 LET CH=A: RETURN

5300 REM *MEGAJUMP*
5310 LET Y=B: GO SUB 820: RETURN

5400 REM *PATHTWIST*
5410 LET L(M)=G: RETURN

5500 REM *DISAPPEAR*
5510 LET L(K)=11: RETURN

5600 REM *MOREMAGIC*
5610 LET L(D)=L(D)+G
5620 PRINT INK H; PAPER B;AT E,D;L(D): RETURN

5700 REM *REZZIRECK*
5710 LET L(H)=L(H)+B: PRINT INK H; PAPER B;AT 12,D;L(H): RETURN

6000 REM *FISTICUFFS*
6005 LET MON =FN R(15): LET LEV=VAL M$(MON,G)
6010 IF Y=B AND LEV=C THEN GO TO 6005
6015 IF Y=C AND LEV=B THEN GO TO 6005
6020 LET NUM=FN R(VAL M$(MON,B))
6100 GO SUB W: PRINT AT 19,C;"You are attacked";AT 20,C;"by denizen": IF NUM>B THEN PRINT AT 20,12;"s"
6105 GO SUB SP
6110 FOR N=12 TO 21: PRINT PAPER F;AT N,20;"           ": NEXT N
6120 LET PIC=32*MON+64120: POKE Z1,PIC-256*INT (PIC/256): POKE Z2,INT (PIC/256): PRINT PAPER H; BRIGHT B;AT 12,20;"AB";AT 13,20;"CD": GO SUB 805
6130 PRINT AT 12,22; PAPER G;M$(MON,J TO )
6140 LET A$="M1 M2 M3": LET E$=A$
6145 IF NUM=C THEN LET E$=A$( TO F)
6150 IF NUM=B THEN LET E$=A$( TO C)
6155 PRINT PAPER F;AT 14,23;E$
6160 RESTORE 8600
6165 FOR N=B TO D: READ A$: PRINT PAPER H;AT 14+N*C,20;A$: NEXT N
6170 DIM W(NUM): DIM X(NUM): DIM Y(NUM)
6175 FOR N=B TO NUM: LET W(N)=VAL M$(MON,H): LET X(N)=VAL M$(MON,C TO D)+FN R(E): LET Y(N)=VAL M$(MON,E TO F)+FN R(E): NEXT N
6180 FOR N=B TO NUM: PRINT INK H; PAPER A;AT 16,N*D+20;W(N);AT 18,N*D+20;X(N);AT 20,N*D+20;Y(N): NEXT N
6182 LET EXPN=A: FOR N=B TO NUM: LET EXPN=EXPN+W(N)+X(N)+Y(N): NEXT N
6200 GO SUB W: LET COM=B: LET BAD=A: LET FIG=A: PRINT AT 19,C;"Magic or Combat?"
6205 LET I$=INKEY$
6210 IF I$="M" THEN GO TO 6250
6215 IF I$="C" THEN GO TO 6600
6220 GO TO 6205
6250 GO SUB W: PRINT AT 19,C;"Choose a spell";AT 20,C;"(A to G)"
6255 LET I$=INKEY$
6260 IF CODE I$<65 OR CODE I$>71 THEN GO TO 6255
6265 GO SUB 5030
6270 IF BAD THEN GO TO 6550
6300 LET COM=A: GO TO (CODE I$-64)*20+6300

6320 REM *NO-FOE*
6325 GO TO 6500

6340 REM *GORLAB*
6345 IF MON<>F THEN GO TO 6480
6350 GO TO 6500

6360 REM *DEMBONES*
6365 IF MON<>13 THEN GO TO 6480
6370 GO TO 6500

6380 REM *ANTIVAMP*
6385 IF MON<>E THEN GO TO 6480
6390 GO TO 6500

6400 REM *BLACKOUT*
6405 IF MON<>D THEN GO TO 6480
6410 GO TO 6500

6420 REM *FIREBALL*
6421 IF NUM=B THEN LET Z=B: GO TO 6428
6423 GO SUB W: PRINT AT 19,B;"On which denizen?"
6424 LET I$=INKEY$
6425 IF CODE I$<49 OR CODE I$>(48+NUM) THEN GO TO 6424
6426 LET Z=(CODE I$-48)
6427 IF NOT Y(Z) THEN GO SUB W: PRINT AT 19,C;"That denizen";AT 20,C;"is dead!": GO SUB SP: GO TO 6550
6428 LET FIRE=FN R(E)+D
6430 LET Y(Z)=Y(Z)-FIRE: GO TO 6970

6440 REM *TELEPORT*
6445 LET TEL=FN R(121): IF TEL=X THEN GO TO 6445
6450 LET X=TEL: GO SUB 820: GO SUB 900: PAPER H: RETURN 
6480 GO SUB W: PRINT AT 19,C;J$: GO SUB SP: GO TO 6550
6500 GO SUB W: PRINT AT 18,C;"You dispose of";AT 19,C;"the foe and gain";AT 20,C;"EXPerience": GO SUB SP
6505 LET L(J)=L(J)+EXPN: PRINT INK H; PAPER B;AT 14,D;L(J): GO SUB 1120: GO SUB 900: RETURN

6550 REM *HOSTILE MEANIES*
6555 IF (MON=B OR MON=F OR MON=G) AND FN R(C)=C AND W(B) THEN GO SUB W: PRINT AT 18,B;"The ";M$(MON,J TO );AT 19,B;"casts a ";K$(FN R(D));AT 20,B;"spell at you": LET W(B)=W(B)-D: PRINT INK H; PAPER A;AT 16,23;W(B): GO SUB SP: GO TO 6560
6556 IF FIG THEN GO TO 6677
6558 GO TO 6200
6560 LET L(F)=L(F)-(FN R(F)): IF L(F)<A THEN LET L(F)=A
6565 PRINT PAPER G;AT J,D;"   "
6570 PRINT INK H; PAPER B;AT J,D;L(F)
6575 IF NOT L(F) THEN GO SUB 6990: LET L(59)=A: LET L(60)=A: GO SUB 6445: RETURN 
6578 IF FIG=B THEN GO TO 6677
6579 IF FIG=C THEN GO TO 6698
6580 GO TO 6200

6600 REM *COMBAT*
6605 GO SUB W: PRINT AT 18,C;"Press SPACE to";AT 19,C;"fight denizen(s)";: GO SUB SP
6610 GO SUB W: LET FIG=B: LET HIT=(L(E)+FN R(F)+FN R(F)+INT (L(56)/E)+INT (L(J)/250))
6615 GO SUB 6940
6620 PRINT AT 19,B;"You attack denizen"
6625 IF NUM>B THEN PRINT AT 20,C;"number ";BOP
6650 GO SUB 6920
6660 IF BRAWL>A THEN GO TO 6720
6665 IF BRAWL<A THEN GO TO 6820
6670 IF NOT BRAWL THEN GO TO 6800

6675 REM *VILLAGER COMBAT*
6676 GO TO 6550
6677 LET FIG=C: GO SUB 6940
6680 FOR P=B TO C
6682 IF L(P+58) THEN LET HIT=J+FN R(F)+FN R(E): GO SUB W: PRINT AT 19,C;"Villager ";P;AT 20,C;"attacks denizen": IF NUM>B THEN PRINT AT 21,C;"number ";BOP
6683 IF NOT L(P+58) THEN GO TO 6698
6684 GO SUB 6920
6686 IF BRAWL<A THEN GO SUB W: PRINT AT 19,C;"Villager ";P;" dies": LET L(58+P)=A: GO SUB 6950: PRINT INK H; PAPER A;AT D,21+P;"X"
6688 IF BRAWL>A THEN GO SUB W: PRINT AT 19,E;"Villager ";P;AT 20,E;"wins round!": GO SUB 6950: LET Y(BOP)=Y(BOP)-BRAWL: LET Z=BOP: GO TO 6970
6692 IF NOT BRAWL THEN GO SUB W: PRINT AT 19,D;"No one hurt": GO SUB 6950
6698 NEXT P
6699 GO TO 6610

6720 REM *WIN ROUND*
6725 GO SUB W: PRINT AT 19,D;"You hit the";AT 20,D;"denizen": LET Y(BOP)=Y(BOP)-BRAWL
6730 IF Y(BOP)>A THEN GO SUB 6950: GO SUB W
6735 IF Y(BOP)<B THEN PRINT AT 21,D;"and kill it!": GO SUB 6950
6745 LET Z=BOP: GO TO 6970

6800 REM *DRAW*
6805 GO SUB W: PRINT AT 19,C;"No one injured": GO SUB 6950: GO TO 6675

6820 REM *LOSE ROUND*
6825 GO SUB W: PRINT AT 19,C;"You are hit";AT 20,C;"by the denizen!": GO SUB 6950: GO SUB W
6830 LET L(F)=L(F)+BRAWL: IF L(F)<A THEN LET L(F)=A
6832 GO TO 6565
6920 LET BRAWL=HIT-(X(BOP)+FN R(F)+FN R(F))
6925 FOR T=B TO F: FOR N=30 TO 25 STEP -1: BEEP .01,N: NEXT N: NEXT T: RETURN 
6940 LET BOP=FN R(NUM): IF NOT Y(BOP) THEN GO TO 6940
6945 RETURN 
6950 FOR N=B TO 100: NEXT N: RETURN

6970 REM *MON END UPDATE*
6972 IF Y(Z)<A THEN LET Y(Z)=A
6974 PRINT PAPER F;AT 20,Z*D+20;"  "; INK H; PAPER A;AT 20,Z*D+20;Y(Z)
6976 LET COUNT=A: FOR N= B TO NUM: IF Y(N) THEN LET COUNT=B
6978 NEXT N
6980 IF NOT COUNT THEN GO TO 6500
6981 IF FIG=B THEN GO TO 6675
6982 IF FIG=C THEN GO TO 6698
6983 GO TO 6550

6989 REM *CHECKDEAD*
6990 LET L(H)=L(H)-B: LET L(F)=FN R(D)+F: IF L(H)<A THEN GO SUB W: PRINT AT 19,C;"Too bad you're";AT 20,C;"a stiff!": BEEP B,-10: GO SUB SP: FOR N=B TO 50: NEXT N: PRINT AT 19,C;"SPACE for another";AT 20,E;"game": GO SUB SP: PAPER G: CLS : GO TO 100
6991 GO SUB W: PRINT AT 19,C;"You lose a life!": BEEP .5,-8: GO SUB 6950: PRINT PAPER G;AT 12,D;"   ";AT J,D;"   "; INK H; PAPER B;AT 12,D;L(H);AT J,D;L(F): GO SUB SP
6995 RETURN

8000 REM *DATA*
8100 DATA "Kelleck","Tamaria","Galodus"
8200 DATA "1070716Sorceror","3060530Gargoyle","1111520Wight"
8205 DATA "1104020Vampire","1121223Balrog","1080839Wizard"
8210 DATA "2111010Ogre","3020330Giant Rat","3050530Orc"
8215 DATA "3060510Kobold","3070630Goblin","3060410Mu-Badger"
8220 DATA "3050230Skeleton","1111330Bludbeest","1101530Slimemold"
8300 DATA "1Thieves'","1Barbarians'","1Magicians'","1Clerics'","1Druids'","1Rangers'","1Assassins'","1Monks'","1Minstrels'","1Alchemists'","1Sorcerors'","1Wizards'","1Paladins'"
8305 DATA B$,B$,B$,B$,B$,B$
8310 DATA "3Moon Tower","3Silver Tower","3Earth Tower"
8315 DATA C$,C$,C$,C$
8320 DATA "5The Abbey","6Deathmeet","6Threewaters","6Rockstream","6Merrydown","6Ludbury"
8325 DATA "7Aardvark Inn","7Jolly Tinker","7Balrog Arms"
8330 DATA D$,D$,D$,D$
8350 DATA "8No-Foe","7Gorlab","2Dembones","5Antivamp","6Blackout","3Fireball","4Teleport","4Charmer","6Megajump","4Pathtwist","4Disappear","3Moremagic","5Rezzireck"
8360 DATA "ABC","DEF","GHI","JKL","MNO","PQQ","RSQ","TUV"
8370 DATA "Fireball","Blaster","Lightning"
8410 DATA FN R(D)+H,FN R(E)+F,FN R(D)+F,FN R(D)+B,E
8500 DATA "MAG","STR","END","PER","RES","EXP","ESA"
8600 DATA "MA:","ST:","EN:"
9998 CLEAR : SAVE "PORTALS" LINE 1: SAVE "PORTCODE"CODE 64672,864: VERIFY "PORTALS": VERIFY "PORTCODE"CODE : STOP : REM TAPE*
9999 CLEAR : SAVE "PORTALS" LINE 1: SAVE "PORTCODE"CODE 64672,864: REM DISC*
